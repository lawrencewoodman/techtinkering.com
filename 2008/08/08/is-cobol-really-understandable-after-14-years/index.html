<!DOCTYPE html>
<html>
  <head>
  <meta charset="utf-8">
  <meta http-equiv="X-UA-Compatible" content="IE=edge">
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <!-- The above 3 meta tags *must* come first in the head; any other head content must come *after* these tags -->
  <title>Is Cobol Really Understandable After 14 Years?</title>
  <meta name="description" content="COBOL has been in the news quite a lot recently and I have been reading that there are still huge amounts of COBOL code running and being written.  This led me t">
  <meta name="keywords" content="Retro Vintage Programming Tinkering"/>

  <!-- *** Twitter Card *** -->
  <meta name="twitter:card" content="summary">
  <meta name="twitter:site" content="@techtinkering">
  <meta name="twitter:title" content="Is Cobol Really Understandable After 14 Years?">
  <meta name="twitter:description" content="COBOL has been in the news quite a lot recently and I have been reading that there are still huge amounts of COBOL code running and being written.  This led me t">
  <!-- *** Twitter Card *** -->

  <!-- *** Open Graph *** --->
  <meta property="og:type" content="website">
  <meta property="og:title" content="Is Cobol Really Understandable After 14 Years?">
  <meta property="og:description" content="COBOL has been in the news quite a lot recently and I have been reading that there are still huge amounts of COBOL code running and being written.  This led me t">
  <meta property="og:url" content="https://techtinkering.com/2008/08/08/is-cobol-really-understandable-after-14-years/">
  <!-- *** Open Graph *** --->

  <!-- Global site tag (gtag.js) - Google Analytics -->
  <script async src="https://www.googletagmanager.com/gtag/js?id=UA-4388762-2"></script>
  <script>
    window.dataLayer = window.dataLayer || [];
    function gtag(){dataLayer.push(arguments);}
    gtag('js', new Date());

    gtag('config', 'UA-4388762-2');
  </script>

  <link href="/css/main.css" rel="stylesheet">
  <link href="/css/article.css" rel="stylesheet">
  <link href="https://cdnjs.cloudflare.com/ajax/libs/cookieconsent2/3.1.0/cookieconsent.min.css" rel="stylesheet" type="text/css" />
  <link rel="canonical" href="https://techtinkering.com/2008/08/08/is-cobol-really-understandable-after-14-years/">
  <link rel="alternate" type="application/rss+xml" title="TechTinkering - Retro Computers, Programming, General Technical Tinkering"
        href="https://feedproxy.google.com/TechTinkering" />

	<!-- ****** faviconit.com favicons ****** -->
	<link rel="shortcut icon" href="/favicon.ico">
	<link rel="icon" sizes="16x16 32x32 64x64" href="/favicon.ico">
	<!-- ****** faviconit.com favicons ****** -->

  <script src="https://cdnjs.cloudflare.com/ajax/libs/cookieconsent2/3.1.0/cookieconsent.min.js"></script>
  <script async src="https://pagead2.googlesyndication.com/pagead/js/adsbygoogle.js"></script>

  <!-- HTML5 shim and Respond.js for IE8 support of HTML5 elements and media queries -->
  <!-- WARNING: Respond.js doesn't work if you view the page via file:// -->
  <!--[if lt IE 9]>
    <script src="https://oss.maxcdn.com/html5shiv/3.7.2/html5shiv.min.js"></script>
    <script src="https://oss.maxcdn.com/respond/1.4.2/respond.min.js"></script>
  <![endif]-->

  <script>
    // Cookie Consent - https://cookieconsent.insites.com
    window.addEventListener("load", function(){
    window.cookieconsent.initialise({
      "palette": {
        "popup": {
          "background": "#252e39"
        },
        "button": {
          "background": "#14a7d0"
        }
      },
      "theme": "classic",
      "position": "top",
      "static": true,
      "content": {
        "message": "TechTinkering uses cookies for advertising, analytics, etc.",
        "href": "https://techtinkering.com/privacy/"
      }
    })});
  </script>

  <script>
    (adsbygoogle = window.adsbygoogle || []).push({
         google_ad_client: "ca-pub-1613732348909658",
         enable_page_level_ads: true
    });
  </script>
</head>
  <body>
    <nav>
  <div class="container">
    <div class="navbar-header">
      <a href="/">TechTinkering</a><br />
      =============
    </div>

    <div class="contact">
      <ul>
        <li>
          <a href="https://twitter.com/TechTinkering" title="@TechTinkering on Twitter">Twitter</a>
        </li>
        <li>
          <a href="https://youtube.com/techtinkering" title="TechTinkering's YouTube Channel">YouTube</a>
        </li>
        <li>
          <a href="https://feeds.feedburner.com/TechTinkering" title="Subscribe to our RSS Feed">RSS</a>
        </li>
        <li>
          <a href="mailto:techtinkering@vlifesystems.com" title="Email us, we'd love to hear from you">Email</a>
        </li>
        <li>
          <a href="/useful-links/" title="Useful vintage computer links">Useful Links</a>
        </li>
      </ul>
    </div>
  </div>
</nav>
    <div id="content" class="container">
      <div class="margin-buffer">
    <article itemscope itemtype="http://schema.org/BlogPosting">
      <header>
        <a href="/2008/08/08/is-cobol-really-understandable-after-14-years/" title="Is Cobol Really Understandable After 14 Years?">
          <h1 itemprop="headline name">Is Cobol Really Understandable After 14 Years?</h1>
        </a>
        <div>
            <time itemprop="datePublished"
                  datetime="2008-08-08">
               8 August 2008
            </time>
          <span itemscope itemprop="publisher"
                itemtype="http://schema.org/Organization">
            <meta itemprop="name" content="TechTinkering" />
            <meta itemprop="url" content="https://techtinkering.com" />
          </span>
          <span itemscope itemprop="author" itemtype="http://schema.org/Person">
            <a rel="author" itemprop="url" href="https://lawrencewoodman.github.io">
              <span itemprop="name">Lawrence Woodman</span>
            </a>
          </span>

          <ul class="tags">
              <li>#<a href="/articles/tag/cobol/">Cobol</a></li>
              <li>#<a href="/articles/tag/programming/">Programming</a></li>
              <li>#<a href="/articles/tag/retro/">Retro</a></li>
          </ul>
        </div>
      </header>
        <div itemprop="articleBody">
        <p>COBOL has been in the news quite a lot recently and I have been reading that there are still huge amounts of COBOL code running and being written.  This led me to wonder why this language was still being used.  I therefore decided to look at a few sites about COBOL and see what they said was good about the language.  The main benefits appeared to be that it is portable and self-documenting.  Indeed, I often read about how COBOL programmers say that they can go to code written 10-15 years ago and still easily understand what is happening.</p>
<p>In 1994, while at college, I did a year of COBOL.  I haven't touched it since that time, and have barely even thought about it.  I therefore thought that this would be a good test.  I admit that the premise above, about the ease of understanding code written a long time ago, refers to people with more COBOL experience than I.  However, I was curious to see how much I would understand.</p>
<p>The code I have chosen was originally written to run under MS-DOS, but unfortunately I can't remember which compiler was used.  It probably isn't the best COBOL code ever written, but I hope that it will help me to explore how easy it is to return to old code.  At times throughout this article I may well refer to things by the wrong name, please bear in mind that the purpose of the article is to test the premise &quot;Is COBOL really understandable after 14 years?&quot;, not to teach COBOL.</p>
<p>I have put the full code, associated data file, example output and instructions on how to compile and run the program at the bottom of this article.</p>
<h2>Analysis of the code</h2>
<p>After taking a quick look at the code I remember that COBOL is split into DIVISIONs, SECTIONs and paragraphs.  I will go through each DIVISION in turn and try to explain what is happening.  I have purposely not looked up anything while doing this to try and test the premise fully.</p>
<h3>The IDENTIFICATION DIVISION</h3>
<p>This is fairly unremarkable and is just used to identify what the code is and who wrote it.</p>
<pre><code class="language-COBOL">IDENTIFICATION DIVISION.
PROGRAM-ID. ASSIGN1.
AUTHOR. xxxxxxxxxx
* Assignment 1 for COBOL.
</code></pre>
<h3>The ENVIRONMENT DIVISION</h3>
<p>This DIVISION is used to specify the environment in which the code will run.  The power of this DIVISION is that you can easily change the environment in which the program is running just by making alterations here.</p>
<pre><code class="language-COBOL">ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
SELECT TAG2-FILE ASSIGN TO &quot;TAG2.DAT&quot;.
SELECT REP-FILE ASSIGN TO &quot;PRN&quot;.
</code></pre>
<p>Within the above DIVISION we can see the INPUT-OUTPUT SECTION and within that we can see the FILE-CONTROL paragraph.  This is telling the program what names to use to refer to the files and what those files are called.  I can see from this that we have a file called TAG2.DAT and another called PRN.  Under MS-DOS, PRN referred to the printer.  However when this is run under Linux it will just create a file called PRN.</p>
<h3>The DATA DIVISION</h3>
<p>This DIVISION specifies how data is stored and structured.</p>
<pre><code class="language-COBOL">DATA DIVISION.
FILE SECTION.
FD TAG2-FILE.
01 TAG2-RCD.
  03 TAG2-ORD-NBR  PIC 9(4).
  03 TAG2-ACC-NBR  PIC A9(7)A.
  03 TAG2-COLOUR   PIC A(3).
  03 TAG2-PRN      PIC A(2).
  03 TAG2-QTY      PIC 9(2).
  03 TAG2-TAG      PIC A(20).
  03 TAG2-DATE     PIC X(6).

FD REP-FILE.
01 REP-RCD.
  03 REP-ACC-NBR   PIC A9(7)A.
  03 FILLER        PIC X(7)       VALUE SPACES.
  03 REP-TAG       PIC A(20).
  03 FILLER        PIC X(9)       VALUE SPACES.
  03 REP-PRN       PIC AA.
  03 FILLER        PIC X(6)       VALUE SPACES.
  03 REP-QTY       PIC Z9.
  03 FILLER        PIC X(6)       VALUE SPACES.
</code></pre>
<p>The first SECTION here is the FILE SECTION, which is specifying how the files assigned in the ENVIRONMENT DIVISION to TAG2-FILE and REP-FILE are structured.</p>
<p>If we take TAG2-FILE we can see that it's biggest structure is TAG2-RCD which is representing a record.  This record is split into 7 fields.  The PIC statements after the names of the field specify the format of the field.  For TAG2-ORD-NBR this is 9(4) representing 4 numeric characters.  For TAG2-ACC-NBR this is A9(7)A representing an alphabetical character then 7 numeric characters then an alphabetical character.  Finally TAG2-DAT has X(6) representing 6 characters of any type.</p>
<pre><code class="language-COBOL">WORKING-STORAGE SECTION.
   01 WS1-AREA.
      03 WS1-LCNT  PIC 99 VALUE 70.

      03 WS1-DATE.
         05 WS1-YR   PIC XX.
         05 WS1-MT   PIC XX.
         05 WS1-DY   PIC XX.

      03 WS1-HDR-AREA.
         05 WS1-DAY       PIC XX.
         05 FILLER        PIC X VALUE '/'.
         05 WS1-MTH       PIC XX.
         05 FILLER        PIC X VALUE '/'.
         05 WS1-YER       PIC XX.
         05 FILLER        PIC X(10) VALUE SPACES.
         05 FILLER        PIC X(19) VALUE 'NAME TAG REPORT - 2'.
         05 FILLER        PIC X(15) VALUE SPACES.
         05 FILLER        PIC X(4)  VALUE 'PAGE'.
         05 FILLER        PIC X(1)  VALUE SPACES.
         05 WS1-PAGE-NBR  PIC Z9.

     03 WS1-PAGE-EDIT    PIC 99 VALUE 0.

     03 WS1-SUB-HDR.
        05 FILLER        PIC X(19) VALUE SPACES.
        05 FILLER        PIC X(9)  VALUE 'COLOUR - '.
        05 SUB-COLOUR    PIC X(5)  VALUE SPACES.

     03 WS1-SUB-HDR2.
        05 FILLER        PIC X(9)  VALUE 'Acct.Nbr.'.
        05 FILLER        PIC X(7)  VALUE SPACES.
        05 FILLER        PIC X(11) VALUE 'Name on Tag'.
        05 FILLER        PIC X(18) VALUE SPACES.
        05 FILLER        PIC X(4)  VALUE 'Type'.
        05 FILLER        PIC X(4)  VALUE SPACES.
        05 FILLER        PIC X(4)  VALUE 'Qty.'.

     03 WS1-COLOR-CHECK PIC A(3)   VALUE SPACES.

     03 WS1-RESULT1.
        05 FILLER        PIC X(5)  VALUE SPACES.
        05 FILLER        PIC X(23) VALUE
                              'RED.........QUANTITY - '.
        05 WS1-RED-TOTAL PIC ZZZ9.
        05 FILLER        PIC X(7)  VALUE SPACES.
        05 FILLER        PIC X(4)  VALUE 'COST'.
        05 FILLER        PIC X     VALUE SPACE.
        05 WS1-RED-COST  PIC $$$$9.99.

     03 WS1-RESULT2.
        05 FILLER        PIC X(5)  VALUE SPACES.
        05 FILLER        PIC X(23) VALUE
                              'BLUE........QUANTITY - '.
        05 WS1-BLU-TOTAL PIC ZZZ9.
        05 FILLER        PIC X(7)  VALUE SPACES.
        05 FILLER        PIC X(4)  VALUE 'COST'.
        05 FILLER        PIC X     VALUE SPACE.
        05 WS1-BLU-COST  PIC $$$$9.99.

     03 WS1-RESULT3.
        05 FILLER        PIC X(5)  VALUE SPACES.
        05 FILLER        PIC X(23) VALUE
                              'BLACK.......QUANTITY - '.
        05 WS1-BLA-TOTAL PIC ZZZ9.
        05 FILLER        PIC X(7)  VALUE SPACES.
        05 FILLER        PIC X(4)  VALUE 'COST'.
        05 FILLER        PIC X     VALUE SPACE.
        05 WS1-BLA-COST  PIC $$$$9.99.

     03 RESULT-CALC.
        05 WS1-BLU-ADD   PIC 9999 VALUE 0.
        05 WS1-RED-ADD   PIC 9999 VALUE 0.
        05 WS1-BLA-ADD   PIC 9999 VALUE 0.
        05 WS1-BLU-TOT   PIC 9(5)V99 VALUE 0.
        05 WS1-RED-TOT   PIC 9(5)V99 VALUE 0.
        05 WS1-BLA-TOT   PIC 9(5)V99 VALUE 0.

     03 WS1-CONTROL-HDR.
        05 FILLER        PIC X(19) VALUE SPACES.
        05 FILLER        PIC X(9)  VALUE 'CONTROLS'.
        05 FILLER        PIC X(4)  VALUE SPACES.
</code></pre>
<p>The next section is the WORKING-STORAGE SECTION.  This is where the variables are specified.  The variables are set out in the same way as the files.  If we look at RESULT-CALC we can see that it is made up of 6 fields each with an initial value of 0.  The V used in the last 3 PIC statements represents a decimal point.</p>
<p>The variables can be referred to by any of the names specified above and will include each of the subdivisions of that name.  So you can refer to RESULT-CALC and it will also automatically be referring to each of its fields as well, or you can just refer to the fields directly e.g. WS1-BLU-ADD.  There is no need to refer to the fields by way of the record.  Which does mean that you have to be careful about not reusing names.</p>
<h3>The PROCEDURE DIVISION</h3>
<p>This is the DIVISION where all the processing gets done.  It can be split into SECTIONs so that you can have subroutines.  However, this piece of code doesn't have any SECTIONs within the PROCEDURE DIVISION.</p>
<pre><code class="language-COBOL">PROCEDURE DIVISION.
     OPEN INPUT TAG2-FILE.
     OPEN OUTPUT REP-FILE.

* Get the Date
     ACCEPT WS1-DATE FROM DATE.
     MOVE WS1-YR TO WS1-YER.
     MOVE WS1-MT TO WS1-MTH.
     MOVE WS1-DY TO WS1-DAY.
</code></pre>
<p>This is opening the files mentioned earlier for INPUT and OUTPUT.  Then today's date is put into WS1-DATE.  After that, the fields in WS1-DATE are specified directly, e.g. WS1-YR.  These fields are moved into fields from WS1-HDR-AREA, e.g. WS1-YER.  Note the Y2K problem here as it is only using a 6 byte date.</p>
<pre><code class="language-COBOL">A01-OP-LINE.
   READ TAG2-FILE AT END GO TO A90-END.
</code></pre>
<p>A01-OP-LINE is a paragraph label that is used to form a loop.  The next line reads a record from TAG2-FILE and if the end of file is reached it jumps to the paragraph A90-END.</p>
<pre><code class="language-COBOL">* Put Correct Colour Name in Sub Heading
     IF TAG2-COLOUR = 'BLU'
     THEN MOVE SPACES TO SUB-COLOUR
          MOVE 'BLUE' TO SUB-COLOUR
     ELSE IF TAG2-COLOUR = 'RED'
          THEN MOVE SPACES TO SUB-COLOUR
               MOVE 'RED' TO SUB-COLOUR
          ELSE MOVE SPACES TO SUB-COLOUR
               MOVE 'BLACK' TO SUB-COLOUR.
</code></pre>
<p>This puts the colour of the tag record read in from the TAG2-FILE into SUB-COLOUR which is a field of WS1-SUB-HDR.</p>
<pre><code class="language-COBOL">* If the number of lines printed &gt; 57 then put up the page heading
     IF WS1-LCNT &gt; 57
     THEN ADD 1 TO WS1-PAGE-EDIT
          MOVE &quot;  &quot; TO WS1-PAGE-NBR
          MOVE WS1-PAGE-EDIT TO WS1-PAGE-NBR
          WRITE REP-RCD FROM WS1-HDR-AREA AFTER ADVANCING PAGE
          MOVE ZERO TO WS1-LCNT
* Output Colour Sub Heading
          MOVE SPACES TO WS1-COLOR-CHECK
          MOVE TAG2-COLOUR TO WS1-COLOR-CHECK
          WRITE REP-RCD FROM WS1-SUB-HDR AFTER ADVANCING 3 LINES
          WRITE REP-RCD FROM WS1-SUB-HDR2 AFTER ADVANCING 1 LINE
          ADD 4 TO WS1-LCNT
          MOVE SPACES TO REP-RCD.
</code></pre>
<p>This prints a header for the page if more than 57 lines have been printed and also prints a heading for the current colour table.  WS1-LCNT is initialised with the value of 70 in the WORKING-STORAGE SECTION, so a header is printed on the first page.  The code if fairly self explanatory.  The MOVE statements copy the literal or variable being referred to into the variable specified.  SPACES represent a full field of space characters.  ZERO is the numerical literal 0.  WRITE REP-RCD... writes the record specified by FROM to the file REP-FILE which is associated with REP-RCD.  The THEN statement will execute everything up to the period, this is called a sentence.</p>
<pre><code class="language-COBOL">IF WS1-COLOR-CHECK = TAG2-COLOUR
THEN NEXT SENTENCE
ELSE MOVE SPACES TO WS1-COLOR-CHECK
    MOVE TAG2-COLOUR TO WS1-COLOR-CHECK
    WRITE REP-RCD FROM WS1-SUB-HDR AFTER ADVANCING 3 LINES
    WRITE REP-RCD FROM WS1-SUB-HDR2 AFTER ADVANCING 1 LINE
    ADD 4 TO WS1-LCNT
    MOVE SPACES TO REP-RCD.
</code></pre>
<p>This checks if the record just read from TAG2-FILE is a different colour from the current table colour.  If the colour is the same then it skips to the NEXT SENTENCE, i.e. after the period.  Otherwise it prints a heading for the table.  The AFTER ADVANCING 3 LINES outputs 3 newlines to the file before writing the record.</p>
<pre><code class="language-COBOL">MOVE TAG2-ACC-NBR TO REP-ACC-NBR.
MOVE TAG2-TAG TO REP-TAG.
MOVE TAG2-PRN TO REP-PRN.
MOVE TAG2-QTY TO REP-QTY.
WRITE REP-RCD AFTER ADVANCING 1 LINE.
</code></pre>
<p>This copies some of the fields of the record TAG2-RCD read in from TAG2-FILE to some of the fields of the record REP-RCD.  The REP-RCD record is then written to the associated REP-FILE.  If you look at REP-RCD in the FILE SECTION you will see that each field is seperated by FILLER spaces.  This is so that a neat table can be built for output.</p>
<pre><code class="language-COBOL">* Add up Tag Quantity for each Colour
     IF TAG2-COLOUR = 'BLU'
     THEN ADD TAG2-QTY TO WS1-BLU-ADD
     ELSE IF TAG2-COLOUR = 'RED'
          THEN ADD TAG2-QTY TO WS1-RED-ADD
          ELSE ADD TAG2-QTY TO WS1-BLA-ADD.
</code></pre>
<p>This determines which colour the current record is from TAG2-COLOUR, then adds the quantity of the current record to the relevant variable.  E.g. for the case of TAG2-COLOUR = 'BLU' the quantity TAG2-QTY is added to the variable WS1-BLU-ADD.</p>
<pre><code class="language-COBOL">* Add up Tag Cost for each Colour
     IF TAG2-COLOUR = 'BLU' AND TAG2-QTY = 24
     THEN ADD 2.25 TO WS1-BLU-TOT
     ELSE IF TAG2-COLOUR = 'BLU' AND TAG2-QTY = 36
          THEN ADD 3.00 TO WS1-BLU-TOT.

     IF TAG2-COLOUR = 'RED' AND TAG2-QTY = 24
     THEN ADD 2.25 TO WS1-RED-TOT
     ELSE IF TAG2-COLOUR = 'RED' AND TAG2-QTY = 36
          THEN ADD 3.00 TO WS1-RED-TOT.

     IF TAG2-COLOUR = 'BLA' AND TAG2-QTY = 24
     THEN ADD 2.25 TO WS1-BLA-TOT
     ELSE IF TAG2-COLOUR = 'BLA' AND TAG2-QTY = 36
          THEN ADD 3.00 TO WS1-BLA-TOT.
</code></pre>
<p>This determines the cost to add to each colour's total.  There are a choice of two quantities than can be ordered.  Either 24 or 36.  It can be seen that here the code should be re-written to have the costs for each quantity stored as a variable for each colour.</p>
<pre><code class="language-COBOL">ADD 1 TO WS1-LCNT.
   GO TO A01-OP-LINE.
A90-END.
</code></pre>
<p>This adds 1 to the line count (WS1-LCNT).  Then jumps back to paragraph label A01-OP-LINE.  A90-END is a paragraph label which is used near the start of the code to jump to when the end of the file TAG2-FILE is reached.</p>
<pre><code class="language-COBOL">* Print Control Report
     MOVE WS1-BLU-ADD TO WS1-BLU-TOTAL.
     MOVE WS1-RED-ADD TO WS1-RED-TOTAL.
     MOVE WS1-BLA-ADD TO WS1-BLA-TOTAL.
     MOVE WS1-BLU-TOT TO WS1-BLU-COST.
     MOVE WS1-RED-TOT TO WS1-RED-COST.
     MOVE WS1-BLA-TOT TO WS1-BLA-COST.
     WRITE REP-RCD FROM WS1-CONTROL-HDR AFTER ADVANCING 4 LINES.
     WRITE REP-RCD FROM WS1-RESULT1 AFTER ADVANCING 1 LINE.
     WRITE REP-RCD FROM WS1-RESULT2 AFTER ADVANCING 1 LINE.
     WRITE REP-RCD FROM WS1-RESULT3 AFTER ADVANCING 1 LINE.
     CLOSE TAG2-FILE REP-FILE.
     STOP RUN.
</code></pre>
<p>The code finishes by copying the running cost and quantity totals to the fields in the records WS1-RESULT1, WS1-RESULT2, WS1-RESULT3.  Then writing a header to REP-FILE using WS-CONTROL-HDR, and writing the quantity and cost for each colour.  The files are then closed and the program is stopped.</p>
<h2>Summary of the program</h2>
<p>The program reads in name tag records from TAG2.DAT and outputs a report to PRN.</p>
<h2>Conclusion</h2>
<p>Once I had remembered how COBOL is set out, I found this program easy to understand and believe that it would be easy to maintain and expand.  I admit that this is only a trivial example, but then I haven't used COBOL for a long time, so I think it is a fair investigation.  I can see from the code why COBOL is so good at processing transactions and in particular batch processing.  A similar program written in Java, the new standard in the business world, would be considerably more complex and difficult to understand.  That said, I do recognise that while most of the added complexity would be with the code to read in the structured input file, the output report writing would be considerably easier by just using formatted strings.</p>
<h2>Afterwards</h2>
<p>After writing this article I decided to look at COBOL in more depth.  My main conclusion is that COBOL has advanced quite considerably since this code was written.  In fact I think that the style of COBOL we were taught in 1994 was already quite dated.  I have enjoyed this trip down memory lane, but now want to see if COBOL can offer anything for the present and into the future.  The most recent advance in COBOL appear to be an object orientated standard, which offers some considerable improvements while maintaining backwards compatibility.  Unfortunately the uptake on this seems to be slow.  COBOL does have a bad press, and after looking at modern COBOL, most of the complaints refer to problems that have been fixed over 30 years ago.  If COBOL is to halt its decline it needs more vocal advocates to show why it is so good and to help explain the COBOL mindset.  One further problem COBOL has long had is the lack of free compilers.  There are a couple of projects out there but they need support.  The most advanced project for Linux appears to be <a href="http://opencobol.org">OpenCOBOL</a>.  Another project which, though incomplete, should offer a great compiler in the long term is <a href="http://cobolforgcc.sourceforge.net/">Cobol for GCC</a>.  I wish these projects well, and hope that COBOL can regain the respect it deserves.</p>
<hr />
<h2>The full code (ASSIGN1.CBL)</h2>
<pre><code class="language-COBOL">     IDENTIFICATION DIVISION.
     PROGRAM-ID. ASSIGN1.
     AUTHOR. Lawrence Woodman
    * Assignment 1 for COBOL.

     ENVIRONMENT DIVISION.
     INPUT-OUTPUT SECTION.
     FILE-CONTROL.
     SELECT TAG2-FILE ASSIGN TO &quot;TAG2.DAT&quot;.
     SELECT REP-FILE ASSIGN TO &quot;PRN&quot;.

     DATA DIVISION.
     FILE SECTION.
     FD TAG2-FILE.
     01 TAG2-RCD.
        03 TAG2-ORD-NBR  PIC 9(4).
        03 TAG2-ACC-NBR  PIC A9(7)A.
        03 TAG2-COLOUR   PIC A(3).
        03 TAG2-PRN      PIC A(2).
        03 TAG2-QTY      PIC 9(2).
        03 TAG2-TAG      PIC A(20).
        03 TAG2-DATE     PIC X(6).

     FD REP-FILE.
     01 REP-RCD.
        03 REP-ACC-NBR   PIC A9(7)A.
        03 FILLER        PIC X(7)       VALUE SPACES.
        03 REP-TAG       PIC A(20).
        03 FILLER        PIC X(9)       VALUE SPACES.
        03 REP-PRN       PIC AA.
        03 FILLER        PIC X(6)       VALUE SPACES.
        03 REP-QTY       PIC Z9.
        03 FILLER        PIC X(6)       VALUE SPACES.

      WORKING-STORAGE SECTION.
         01 WS1-AREA.
            03 WS1-LCNT  PIC 99 VALUE 70.

            03 WS1-DATE.
               05 WS1-YR   PIC XX.
               05 WS1-MT   PIC XX.
               05 WS1-DY   PIC XX.

            03 WS1-HDR-AREA.
               05 WS1-DAY       PIC XX.
               05 FILLER        PIC X VALUE '/'.
               05 WS1-MTH       PIC XX.
               05 FILLER        PIC X VALUE '/'.
               05 WS1-YER       PIC XX.
               05 FILLER        PIC X(10) VALUE SPACES.
               05 FILLER        PIC X(19) VALUE 'NAME TAG REPORT - 2'.
               05 FILLER        PIC X(15) VALUE SPACES.
               05 FILLER        PIC X(4)  VALUE 'PAGE'.
               05 FILLER        PIC X(1)  VALUE SPACES.
               05 WS1-PAGE-NBR  PIC Z9.

           03 WS1-PAGE-EDIT    PIC 99 VALUE 0.

           03 WS1-SUB-HDR.
              05 FILLER        PIC X(19) VALUE SPACES.
              05 FILLER        PIC X(9)  VALUE 'COLOUR - '.
              05 SUB-COLOUR    PIC X(5)  VALUE SPACES.

           03 WS1-SUB-HDR2.
              05 FILLER        PIC X(9)  VALUE 'Acct.Nbr.'.
              05 FILLER        PIC X(7)  VALUE SPACES.
              05 FILLER        PIC X(11) VALUE 'Name on Tag'.
              05 FILLER        PIC X(18) VALUE SPACES.
              05 FILLER        PIC X(4)  VALUE 'Type'.
              05 FILLER        PIC X(4)  VALUE SPACES.
              05 FILLER        PIC X(4)  VALUE 'Qty.'.

           03 WS1-COLOR-CHECK PIC A(3)   VALUE SPACES.

           03 WS1-RESULT1.
              05 FILLER        PIC X(5)  VALUE SPACES.
              05 FILLER        PIC X(23) VALUE
                                    'RED.........QUANTITY - '.
              05 WS1-RED-TOTAL PIC ZZZ9.
              05 FILLER        PIC X(7)  VALUE SPACES.
              05 FILLER        PIC X(4)  VALUE 'COST'.
              05 FILLER        PIC X     VALUE SPACE.
              05 WS1-RED-COST  PIC $$$$9.99.

           03 WS1-RESULT2.
              05 FILLER        PIC X(5)  VALUE SPACES.
              05 FILLER        PIC X(23) VALUE
                                    'BLUE........QUANTITY - '.
              05 WS1-BLU-TOTAL PIC ZZZ9.
              05 FILLER        PIC X(7)  VALUE SPACES.
              05 FILLER        PIC X(4)  VALUE 'COST'.
              05 FILLER        PIC X     VALUE SPACE.
              05 WS1-BLU-COST  PIC $$$$9.99.

           03 WS1-RESULT3.
              05 FILLER        PIC X(5)  VALUE SPACES.
              05 FILLER        PIC X(23) VALUE
                                    'BLACK.......QUANTITY - '.
              05 WS1-BLA-TOTAL PIC ZZZ9.
              05 FILLER        PIC X(7)  VALUE SPACES.
              05 FILLER        PIC X(4)  VALUE 'COST'.
              05 FILLER        PIC X     VALUE SPACE.
              05 WS1-BLA-COST  PIC $$$$9.99.

           03 RESULT-CALC.
              05 WS1-BLU-ADD   PIC 9999 VALUE 0.
              05 WS1-RED-ADD   PIC 9999 VALUE 0.
              05 WS1-BLA-ADD   PIC 9999 VALUE 0.
              05 WS1-BLU-TOT   PIC 9(5)V99 VALUE 0.
              05 WS1-RED-TOT   PIC 9(5)V99 VALUE 0.
              05 WS1-BLA-TOT   PIC 9(5)V99 VALUE 0.

           03 WS1-CONTROL-HDR.
              05 FILLER        PIC X(19) VALUE SPACES.
              05 FILLER        PIC X(9)  VALUE 'CONTROLS'.
              05 FILLER        PIC X(4)  VALUE SPACES.

     PROCEDURE DIVISION.
         OPEN INPUT TAG2-FILE.
         OPEN OUTPUT REP-FILE.

    * Get the Date
         ACCEPT WS1-DATE FROM DATE.
         MOVE WS1-YR TO WS1-YER.
         MOVE WS1-MT TO WS1-MTH.
         MOVE WS1-DY TO WS1-DAY.

     A01-OP-LINE.
         READ TAG2-FILE AT END GO TO A90-END.

    * Put Correct Colour Name in Sub Heading
         IF TAG2-COLOUR = 'BLU'
         THEN MOVE SPACES TO SUB-COLOUR
              MOVE 'BLUE' TO SUB-COLOUR
         ELSE IF TAG2-COLOUR = 'RED'
              THEN MOVE SPACES TO SUB-COLOUR
                   MOVE 'RED' TO SUB-COLOUR
              ELSE MOVE SPACES TO SUB-COLOUR
                   MOVE 'BLACK' TO SUB-COLOUR.

    * If the number of lines printed &gt; 57 then put up the page heading
         IF WS1-LCNT &gt; 57
         THEN ADD 1 TO WS1-PAGE-EDIT
              MOVE &quot;  &quot; TO WS1-PAGE-NBR
              MOVE WS1-PAGE-EDIT TO WS1-PAGE-NBR
              WRITE REP-RCD FROM WS1-HDR-AREA AFTER ADVANCING PAGE
              MOVE ZERO TO WS1-LCNT
    * Output Colour Sub Heading
              MOVE SPACES TO WS1-COLOR-CHECK
              MOVE TAG2-COLOUR TO WS1-COLOR-CHECK
              WRITE REP-RCD FROM WS1-SUB-HDR AFTER ADVANCING 3 LINES
              WRITE REP-RCD FROM WS1-SUB-HDR2 AFTER ADVANCING 1 LINE
              ADD 4 TO WS1-LCNT
              MOVE SPACES TO REP-RCD.

         IF WS1-COLOR-CHECK = TAG2-COLOUR
         THEN NEXT SENTENCE
         ELSE MOVE SPACES TO WS1-COLOR-CHECK
              MOVE TAG2-COLOUR TO WS1-COLOR-CHECK
              WRITE REP-RCD FROM WS1-SUB-HDR AFTER ADVANCING 3 LINES
              WRITE REP-RCD FROM WS1-SUB-HDR2 AFTER ADVANCING 1 LINE
              ADD 4 TO WS1-LCNT
              MOVE SPACES TO REP-RCD.

         MOVE TAG2-ACC-NBR TO REP-ACC-NBR.
         MOVE TAG2-TAG TO REP-TAG.
         MOVE TAG2-PRN TO REP-PRN.
         MOVE TAG2-QTY TO REP-QTY.
         WRITE REP-RCD AFTER ADVANCING 1 LINE.

    * Add up Tag Quantity for each Colour
         IF TAG2-COLOUR = 'BLU'
         THEN ADD TAG2-QTY TO WS1-BLU-ADD
         ELSE IF TAG2-COLOUR = 'RED'
              THEN ADD TAG2-QTY TO WS1-RED-ADD
              ELSE ADD TAG2-QTY TO WS1-BLA-ADD.

    * Add up Tag Cost for each Colour
         IF TAG2-COLOUR = 'BLU' AND TAG2-QTY = 24
         THEN ADD 2.25 TO WS1-BLU-TOT
         ELSE IF TAG2-COLOUR = 'BLU' AND TAG2-QTY = 36
              THEN ADD 3.00 TO WS1-BLU-TOT.

         IF TAG2-COLOUR = 'RED' AND TAG2-QTY = 24
         THEN ADD 2.25 TO WS1-RED-TOT
         ELSE IF TAG2-COLOUR = 'RED' AND TAG2-QTY = 36
              THEN ADD 3.00 TO WS1-RED-TOT.

         IF TAG2-COLOUR = 'BLA' AND TAG2-QTY = 24
         THEN ADD 2.25 TO WS1-BLA-TOT
         ELSE IF TAG2-COLOUR = 'BLA' AND TAG2-QTY = 36
              THEN ADD 3.00 TO WS1-BLA-TOT.

         ADD 1 TO WS1-LCNT.
         GO TO A01-OP-LINE.
     A90-END.
    * Print Control Report
         MOVE WS1-BLU-ADD TO WS1-BLU-TOTAL.
         MOVE WS1-RED-ADD TO WS1-RED-TOTAL.
         MOVE WS1-BLA-ADD TO WS1-BLA-TOTAL.
         MOVE WS1-BLU-TOT TO WS1-BLU-COST.
         MOVE WS1-RED-TOT TO WS1-RED-COST.
         MOVE WS1-BLA-TOT TO WS1-BLA-COST.
         WRITE REP-RCD FROM WS1-CONTROL-HDR AFTER ADVANCING 4 LINES.
         WRITE REP-RCD FROM WS1-RESULT1 AFTER ADVANCING 1 LINE.
         WRITE REP-RCD FROM WS1-RESULT2 AFTER ADVANCING 1 LINE.
         WRITE REP-RCD FROM WS1-RESULT3 AFTER ADVANCING 1 LINE.
         CLOSE TAG2-FILE REP-FILE.
         STOP RUN.
</code></pre>
<h2>Uuencode data file (TAG2.DAT)</h2>
<pre><code class="language-text">begin-base64 644 TAG2.DAT
MDAwMWEwMDAwMDAxYVJFRFNDMjRKaiAgICAgICAgICAgICAgICAgIDk0MTEy
MjAwMDJhMDAwMDAwMmFSRURTQzM2RC5Ccm93biAgICAgICAgICAgICA5NDEx
MjIwMDAzYTAwMDAwMDNhUkVEU1QyNFQuSm9uZXMgICAgICAgICAgICAgOTQx
MTIyMDAwNGEwMDAwMDA0YVJFRFNUMzZSLlRhbGJvdCAgICAgICAgICAgIDk0
MTEyMjAwMDVhMDAwMDAwNWFCTFVTQzI0Ui5KICAgICAgICAgICAgICAgICA5
NDExMjIwMDA2YTAwMDAwMDZhQkxVU0MzNkguTCAgICAgICAgICAgICAgICAg
OTQxMTIyMDAwN2EwMDAwMDA3YUJMVVNUMjRSLlAgICAgICAgICAgICAgICAg
IDk0MTEyMjAwMDhhMDAwMDAwOGFCTFVTVDM2Si5LICAgICAgICAgICAgICAg
ICA5NDExMjIwMDA5YTAwMDAwMDlhQkxBU0MyNFIuUCAgICAgICAgICAgICAg
ICAgOTQxMTIyMDAxMGEwMDAwMDEwYUJMQVNDMzZQLlAgICAgICAgICAgICAg
ICAgIDk0MTEyMjAwMTFhMDAwMDAxMWFCTEFTVDI0US5RICAgICAgICAgICAg
ICAgICA5NDExMjIwMDEyYTAwMDAwMTJhQkxBU1QzNk8uTyAgICAgICAgICAg
ICAgICAgOTQxMTIyCg==
====
</code></pre>
<h2>Compiling and running the program</h2>
<ul>
<li>To compile this code I am using <a href="http://opencobol.org">OpenCOBOL</a> under Linux.  I compile it to en executable using: <code>cobc -x ASSIGN1.CBL</code></li>
<li>To create the TAG2.DAT file that is needed copy the uuencoded data above to a file, say tag2.uue.  Then run: <code>uudecode tag2.uue</code></li>
<li>To execute the program, make sure that TAG2.DAT is in the same directory as the executable ASSIGN1.  Then run <code>./ASSIGN1</code></li>
</ul>
<p>After running the program you should find a file called PRN has been created which contains the report.</p>
<h2>Example report (PRN)</h2>
<pre><samp>26/08/08          NAME TAG REPORT - 2               PAGE  1


               COLOUR - RED
Acct.Nbr.       Name on Tag                  Type    Qty.
a0000001a       Jj                           SC      24
a0000002a       D.Brown                      SC      36
a0000003a       T.Jones                      ST      24
a0000004a       R.Talbot                     ST      36


               COLOUR - BLUE
Acct.Nbr.       Name on Tag                  Type    Qty.
a0000005a       R.J                          SC      24
a0000006a       H.L                          SC      36
a0000007a       R.P                          ST      24
a0000008a       J.K                          ST      36


               COLOUR - BLACK
Acct.Nbr.       Name on Tag                  Type    Qty.
a0000009a       R.P                          SC      24
a0000010a       P.P                          SC      36
a0000011a       Q.Q                          ST      24
a0000012a       O.O                          ST      36



               CONTROLS
 RED.........QUANTITY -  120       COST   $10.50
 BLUE........QUANTITY -  120       COST   $10.50
 BLACK.......QUANTITY -  120       COST   $10.50
</samp></pre>
      </div>
    </article>
</div>

  <div id="cclicence">
    <a rel="license" href="http://creativecommons.org/licenses/by/4.0/">
      <img alt="Creative Commons License" style="border-width:0" src="https://i.creativecommons.org/l/by/4.0/88x31.png" />
    </a>
    <br />
    <span xmlns:dct="http://purl.org/dc/terms/" property="dct:title">Is Cobol Really Understandable After 14 Years?</span>
    by <a xmlns:cc="http://creativecommons.org/ns#" href="https://techtinkering.com/2008/08/08/is-cobol-really-understandable-after-14-years/" property="cc:attributionName" rel="cc:attributionURL">Lawrence Woodman</a>
   is licensed under a <a rel="license" href="http://creativecommons.org/licenses/by/4.0/">Creative Commons Attribution 4.0 International License</a>.
   
  </div>


  <div class="discuss">
    <h2 style="margin-top:0;">Share This Post</h2>
    <div class="contact">
  <ul>
    <li><a href="http://www.reddit.com/submit?url=https%3A%2F%2Ftechtinkering.com%2F2008%2F08%2F08%2Fis-cobol-really-understandable-after-14-years%2F&title=Is%20Cobol%20Really%20Understandable%20After%2014%20Years%3F" target="_blank" title="Submit to Reddit">Reddit</a></li>
    <li><a href="https://www.facebook.com/sharer/sharer.php?u=https%3A%2F%2Ftechtinkering.com%2F2008%2F08%2F08%2Fis-cobol-really-understandable-after-14-years%2F&quote=Is%20Cobol%20Really%20Understandable%20After%2014%20Years%3F" target="_blank" title="Share on Facebook">Facebook</a></li>
    <li><a href="https://twitter.com/intent/tweet?source=https%3A%2F%2Ftechtinkering.com%2F2008%2F08%2F08%2Fis-cobol-really-understandable-after-14-years%2F&text=Is%20Cobol%20Really%20Understandable%20After%2014%20Years%3F:%0Ahttps%3A%2F%2Ftechtinkering.com%2F2008%2F08%2F08%2Fis-cobol-really-understandable-after-14-years%2F&via=TechTinkering" target="_blank" title="Tweet">Twitter</a></li>
    <li><a href="mailto:?subject=Is%20Cobol%20Really%20Understandable%20After%2014%20Years%3F&body=COBOL%20has%20been%20in%20the%20news%20quite%20a%20lot%20recently%20and%20I%20have%20been%20reading%20that%20there%20are%20still%20huge%20amounts%20of%20COBOL%20code%20running%20and%20being%20written.%20%20This%20led%20me%20to%20wonder%20why%20this%20language%20was%20still%20bei...:%0A%0Ahttps%3A%2F%2Ftechtinkering.com%2F2008%2F08%2F08%2Fis-cobol-really-understandable-after-14-years%2F" target="_blank" title="Send email">Email</a></li>
  </ul>
</div>

    <h2>Feedback/Discuss</h2>
    <div class="contact">
  <ul>


    <li><a href="mailto:techtinkering@vlifesystems.com?subject=Comment:%20Is%20Cobol%20Really%20Understandable%20After%2014%20Years%3F&body=Article%20URL:%20https%3A%2F%2Ftechtinkering.com%2F2008%2F08%2F08%2Fis-cobol-really-understandable-after-14-years%2F%0A" title="Send us an email">Email</a></li>
  </ul>
</div>
  </div>


    <div>
    <h2>Related Articles</h2>
    <div class="margin-buffer">
      <header>
  <h2><a href="/articles/80-columns-in-software-on-the-commodore-vic-20/">80 Columns in Software on the Commodore VIC-20</a></h2>
  <span class="post-meta">
      <ul class="tags">
          <li>#<a href="/articles/tag/commodore/">Commodore</a></li>
          <li>#<a href="/articles/tag/programming/">Programming</a></li>
          <li>#<a href="/articles/tag/retro/">Retro</a></li>
          <li>#<a href="/articles/tag/vic-20/">VIC-20</a></li>
      </ul>
  </span>
</header>
<div>
  If you have good eyesight, a well-tuned display and patience it is possible to use 80 columns in software on the VIC-20.  This is really just an experiment but considering the limitations of the Vic I ...
  &nbsp;
  <a class="readMore" href="/articles/80-columns-in-software-on-the-commodore-vic-20/">Read More</a>
</div>
<br />
      <header>
  <h2><a href="/articles/64-columns-sideways-on-the-commodore-vic-20/">64 Column Text Mode on the Commodore VIC-20</a></h2>
  <span class="post-meta">
      <ul class="tags">
          <li>#<a href="/articles/tag/commodore/">Commodore</a></li>
          <li>#<a href="/articles/tag/programming/">Programming</a></li>
          <li>#<a href="/articles/tag/retro/">Retro</a></li>
          <li>#<a href="/articles/tag/vic-20/">VIC-20</a></li>
      </ul>
  </span>
</header>
<div>
  With a little lateral thinking and by putting the television on its side we can create a 64 column sideways text mode on the VIC-20.  This article will demonstrate this and show how it is done.  Previo...
  &nbsp;
  <a class="readMore" href="/articles/64-columns-sideways-on-the-commodore-vic-20/">Read More</a>
</div>
<br />
      <header>
  <h2><a href="/articles/getting-the-address-of-basic-variables-on-the-vic-20/">Getting the Address of BASIC Variables on the VIC-20</a></h2>
  <span class="post-meta">
      <ul class="tags">
          <li>#<a href="/articles/tag/assembly/">Assembly</a></li>
          <li>#<a href="/articles/tag/basic/">BASIC</a></li>
          <li>#<a href="/articles/tag/commodore/">Commodore</a></li>
          <li>#<a href="/articles/tag/programming/">Programming</a></li>
          <li>#<a href="/articles/tag/retro/">Retro</a></li>
          <li>#<a href="/articles/tag/vic-20/">VIC-20</a></li>
      </ul>
  </span>
</header>
<div>
  Getting the address of a BASIC variable can be useful if you want to pass data to a machine code routine or want to access the bytes of a variable directly to improve speed and reduce garbage collectio...
  &nbsp;
  <a class="readMore" href="/articles/getting-the-address-of-basic-variables-on-the-vic-20/">Read More</a>
</div>
<br />
      <header>
  <h2><a href="/articles/saving-and-loading-memory-on-the-vic-20/">Saving and Loading Memory on the VIC-20</a></h2>
  <span class="post-meta">
      <ul class="tags">
          <li>#<a href="/articles/tag/assembly/">Assembly</a></li>
          <li>#<a href="/articles/tag/basic/">BASIC</a></li>
          <li>#<a href="/articles/tag/commodore/">Commodore</a></li>
          <li>#<a href="/articles/tag/programming/">Programming</a></li>
          <li>#<a href="/articles/tag/retro/">Retro</a></li>
          <li>#<a href="/articles/tag/vic-20/">VIC-20</a></li>
      </ul>
  </span>
</header>
<div>
  Saving and loading memory is quite easy on the VIC-20 once you know how.  However, it isn't obvious how to do this and therefore this article will present a few simple ways of doing it from BASIC and A...
  &nbsp;
  <a class="readMore" href="/articles/saving-and-loading-memory-on-the-vic-20/">Read More</a>
</div>
<br />
      <header>
  <h2><a href="/articles/basic-line-storage-on-the-vic-20/">Basic Line Storage on the VIC-20</a></h2>
  <span class="post-meta">
      <ul class="tags">
          <li>#<a href="/articles/tag/basic/">BASIC</a></li>
          <li>#<a href="/articles/tag/commodore/">Commodore</a></li>
          <li>#<a href="/articles/tag/programming/">Programming</a></li>
          <li>#<a href="/articles/tag/retro/">Retro</a></li>
          <li>#<a href="/articles/tag/vic-20/">VIC-20</a></li>
      </ul>
  </span>
</header>
<div>
  BASIC programs are stored in memory using a simple structure that we can investigate and manipulate.  This article will show how they are stored and contains a BASIC program to go through each line of ...
  &nbsp;
  <a class="readMore" href="/articles/basic-line-storage-on-the-vic-20/">Read More</a>
</div>
<br />
</div>
    </div>

    </div>

    <footer>
  <nav>
    <div class="container">
      <div>
        <strong>Legal:</strong>
        <a href="/terms/">T & C</a>, <a href="/privacy/">Privacy Policy</a>
        &nbsp;
        <strong>Misc:</strong>
        <a href="https://github.com/lawrencewoodman/techtinkering.com" title="Fork TechTinkering On GitHub ">GitHub</a>

      </div>
    </div>
  </nav>
</footer>
  </body>
</html>